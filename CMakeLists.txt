CMAKE_MINIMUM_REQUIRED(VERSION 2.6)
PROJECT(scalednum)

# parameters
OPTION(BUILD_STATIC "Build static libraries" ON)
OPTION(BUILD_SHARED "Build shared libraries" ON)
OPTION(BUILD_DEMO "Build demo" ON)

# conditions
IF(NOT BUILD_STATIC AND NOT BUILD_SHARED)
  MESSAGE(FATAL_ERROR "Cannot build with both BUILD_STATIC and BUILD_SHARED disabled")
ENDIF()

# Doxygen
FIND_PACKAGE(Doxygen)
OPTION(BUILD_DOCUMENTATION "Create and install API documentation (requires Doxygen)" ${DOXYGEN_FOUND})

# build parameters
#SET(CMAKE_C_FLAGS "-g -Wall")

# determine dependencies
IF(WIN32)
  SET(MATH_LIBRARIES "")
ELSE()
  SET(MATH_LIBRARIES "-lm")
ENDIF()

# build definitions
SET(ALLTARGETS)
SET(LINKTYPES)
IF(BUILD_STATIC)
  LIST(APPEND LINKTYPES "STATIC")
ENDIF()
IF(BUILD_SHARED)
  LIST(APPEND LINKTYPES "SHARED")
ENDIF()
FOREACH(LINKTYPE ${LINKTYPES})
  ADD_LIBRARY(scalednum_${LINKTYPE} ${LINKTYPE} src/scalednum.c)
  SET_TARGET_PROPERTIES(scalednum_${LINKTYPE} PROPERTIES COMPILE_DEFINITIONS "${LINKTYPE}_SCALEDNUM")
  #IF(MSVC AND LINKTYPE STREQUAL "STATIC")
  #  SET_TARGET_PROPERTIES(scalednum_${LINKTYPE} PROPERTIES SUFFIX "_s.lib")
  #ENDIF()
  #IF(MSVC AND LINKTYPE STREQUAL "STATIC")
  #  SET_TARGET_PROPERTIES(scalednum_${LINKTYPE} PROPERTIES OUTPUT_NAME scalednum_s)
  #ELSE()
    SET_TARGET_PROPERTIES(scalednum_${LINKTYPE} PROPERTIES OUTPUT_NAME scalednum)
  #ENDIF()
  IF(MINGW AND LINKTYPE STREQUAL "SHARED")
    SET_TARGET_PROPERTIES(scalednum_${LINKTYPE} PROPERTIES LINK_FLAGS "-Wl,--output-def=libscalednum.def")
    SET_SOURCE_FILES_PROPERTIES(libscalednum.def PROPERTIES HEADER_FILE_ONLY TRUE)
  ENDIF()
  TARGET_INCLUDE_DIRECTORIES(scalednum_${LINKTYPE} PRIVATE src)
  TARGET_INCLUDE_DIRECTORIES(scalednum_${LINKTYPE} PUBLIC include)
  TARGET_LINK_LIBRARIES(scalednum_${LINKTYPE} ${MATH_LIBRARIES})
  SET(ALLTARGETS ${ALLTARGETS} scalednum_${LINKTYPE})

  SET(EXELINKTYPE ${LINKTYPE})
ENDFOREACH()

IF(BUILD_DEMO)
  ADD_EXECUTABLE(scalednum_demo src/scalednum_demo.c)
  TARGET_INCLUDE_DIRECTORIES(scalednum_demo PUBLIC include)
  TARGET_LINK_LIBRARIES(scalednum_demo scalednum_${EXELINKTYPE})
  IF(EXELINKTYPE STREQUAL "STATIC")
    SET_TARGET_PROPERTIES(scalednum_demo PROPERTIES COMPILE_DEFINITIONS "STATIC")
    TARGET_LINK_LIBRARIES(scalednum_demo ${MATH_LIBRARIES})
  ENDIF()
  SET(ALLTARGETS ${ALLTARGETS} scalednum_demo)
ENDIF()

IF(BUILD_DOCUMENTATION)
  IF(NOT DOXYGEN_FOUND)
    MESSAGE(FATAL_ERROR "Doxygen is needed to build the documentation.")
  ENDIF()
  ADD_CUSTOM_TARGET(doc ALL
    COMMAND ${DOXYGEN_EXECUTABLE} ${CMAKE_CURRENT_SOURCE_DIR}/doc/Doxyfile
    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
    COMMENT "Generating API documentation with Doxygen"
    VERBATIM
  )
  INSTALL(DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/doc/man
    DESTINATION .
  )
  INSTALL(DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/doc/html
    DESTINATION share/scalednum
  )
ENDIF()

# installation specifications
INSTALL(TARGETS ${ALLTARGETS}
  ARCHIVE DESTINATION lib
  LIBRARY DESTINATION lib
  RUNTIME DESTINATION bin
)
INSTALL(DIRECTORY include/
  DESTINATION include 
  FILES_MATCHING PATTERN "*.h"
)
IF(MINGW AND BUILD_SHARED)
  INSTALL(FILES "${CMAKE_CURRENT_BINARY_DIR}/libscalednum.def" DESTINATION lib)
ENDIF()
